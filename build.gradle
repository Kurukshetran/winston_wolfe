apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

sourceCompatibility = "1.6"

project.ext {
    integrationTestsJarFiles = fileTree(dir: 'lib/integration_test', include: '*.jar')
    integrationTestsSourceDir = 'src/integration_tests/java'
    integrationTestsResourceDir = 'src/integration_tests/resources'
}

sourceSets {
    integrationTest {
        java.srcDir(integrationTestsSourceDir)
        resources.srcDir(integrationTestsResourceDir)
    }
}

dependencies {
    compile fileTree(dir: 'lib/runtime', include: '*.jar')
    testCompile fileTree(dir: 'lib/test', include: '*.jar')

    integrationTestCompile sourceSets.main.output
    integrationTestCompile fileTree(dir: 'lib/test', include: '*.jar')
    integrationTestCompile integrationTestsJarFiles
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
}

mainClassName = 'com.thoughtworks.winstonwolfe.application.WinstonWolfe'
distZip {
    baseName = 'WinstonWolfe'
}

idea {
    project {
        languageLevel = sourceCompatibility
    }

    module {
        name = 'winston_wolfe'
        testSourceDirs += [ file(integrationTestsSourceDir),
                            file(integrationTestsResourceDir) ]
        dependencies {
            testCompile integrationTestsJarFiles
        }
    }
}
